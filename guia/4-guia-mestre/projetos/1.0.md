

### Projeto de construção de programa simples IO
  
  #### Tipos de dados e atribuicao de dados
  
  > vamos criar um progama que armazena genero e a idade de uma pessoa

~~~ alg
Algoritmo "nome e idade"

  Var
    genero_pessoa: caractere
    idade_pessoa: inteiro

Inicio
    genero_pessoa <- "f"
    idade_pessoa <- 18

FimAlgoritmo
~~~

  #### Tipos de dados e escrevendo com o teclado
  
  > Nosso proximo programa registrar numero de pessoas no onibus

~~~
Algoritmo "lotação do onibus"

Var
  numero_passageiros : inteiro
  
Inicio
  Leia(numero_passageiros)

FimAlgoritmo
~~~

  #### Tipos de dados e exibindo mensagem e dados na tela

  > Nosso programa ira exibir uma mensagem na tela de boas vindas!

~~~
Algoritmo "mensagem na tela"

Var
  
Inicio
  escreva("Ola!")
  Escreval("Bem Vindo ao meu programa.")
    
FimAlgoritmo
~~~ 

  #### Juntando Todo o Conhecimento
  
  > Nosso programa ira registrar a temperatura e Escreva uma mensagem na tela com a temperatura 

~~~
Algoritmo "medindo a temperatura"

Var
  temperatura_ceusius : real

Inicio
  escreva("Digite a Temperatura do Dia: ")
  Leia(temperatura_ceusius)

  Escreval("Temperatura Atual", temperatura_ceusius)
    
FimAlgoritmo
~~~

  #### Projeto Final

  > Nosso programa ira registrar os dados pessoais de alguem que voce conheceu na festa de ontem

~~~
Algoritmo "contato da festa"

Var
  idade: inteiro
  numero_celular: inteiro

Inicio
  escreva("Registrar o Celular: ")
  Leia(numero_celular)
  
  Escreval("Contato Da Festa de Ontem: ")
  escreva("Oi, sou Ursula !","Lige para #",numero_celular)

FimAlgoritmo
~~~


  >  Projeto Final Completo
  > ### Banco Hirão!
  ~~~ alg
Algoritmo "Banco Hirão"

Var
  menu : inteiro
  saldo_conta : real

  Procedimento Saldo (Var conta: real)
  Inicio
    Escreval("Saldo: ", conta)
  FimProcedimento

  Procedimento Saque (Var conta : real)
  Var 
    valor : real
  Inicio
    escreva("Valor para Saque?")
    Leia(valor)

    Se valor > conta Entao
      Escreval("pobre nao pode gastar")
    Senao
      conta <- conta - valor
      Escreval("-", valor, " Sobrou", conta)
    FimSe
  FimProcedimento

  Procedimento Deposito ( Var conta: real)
  Var
    valor : real
  Inicio 
    escreva("Valor para Deposito?")
    Leia(valor)
    conta <- conta + valor
    Escreval("+", valor, " Fortuna:", conta)
  FimProcedimento

  Procedimento TelaInicial
  Inicio
      Escreval("Banco Hirão")
      Escreval("Saldo [1], Saque [2], Deposito [3], Sair [0]")
  FimProcedimento

  Procedimento ModulosPrograma (escolhido : inteiro; Var dinheiro : inteiro)
  Inicio
    LimpaTela
    Escolha (escolhido)
      Caso 1
        Saldo(dinheiro)
      Caso 2
        Saque(dinheiro)
      Caso 3
        Deposito(dinheiro)
      OutroCaso
        Escreval("nao Sei")
    FimEscolha
  FimProcedimento

Inicio
  menu <- 0
  saldo_conta <- 0

  Repita
    TelaInicial    
    Leia(menu)

    Se menu = 0 Entao
      Escreval("saindo do banco")
      Interrompa
    Senao
      ModulosPrograma(menu, saldo_conta)
    FimSe
  Ate falso

FimAlgoritmo
~~~
